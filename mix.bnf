<block> ::= <optional_statement_list>

<optional_statement_list> ::= ""
                            | <optional_statement_list> <statement>
                            | <optional_statement_list> <export_statement>

; --------------------------------------------------------------------------- ;

<statement> ::= ";"
              | <expression> ";"
              | <variable_declaration_statement>
              | <assignment_statement>
              | <selection_statement>
              | <iteration_statement>
              | <jump_statement>
              | <import_statement>
              | <compound_statement>
              | <enum_declaration>
              | <class_definition>
              | <function_definition>

<variable_declaration_statement> ::= "var" <variable_declaration_list> ";"
                                   | "let" <variable_declaration_list> ";"

<assignment_statement> ::= <variable> <assignment_operator> <expression> ";"

<variable> ::= <identifier>
             | <postfix_expression> "[" <expression> "]"
             | <postfix_expression> "." <identifier>

<assignment_operator> ::= "=" | "+=" | "-=" | "*=" | "/=" | "%=" | "&=" | "|=" | "^=" | "<<=" | ">>="

<variable_declaration_list> ::= <variable_declaration>
                              | <variable_declaration_list> "," <variable_declaration>

<variable_declaration> ::= <identifier>
                         | <identifier> "=" <expression>

<selection_statement> ::= "if" <expression> <compound_statement>
                        | "if" <expression> <compound_statement> "else" <selection_statement>
                        | "if" <expression> <compound_statement> "else" <compound_statement>

<iteration_statement> ::= "while" <expression> <compound_statement>
                        | "do" <compound_statement> "while" <expression> ";"
                        | "for" <identifier> "in" <expression> <compound_statement>

<compound_statement> ::= "{" <optional_statement_list> "}"

<jump_statement> ::= "continue" ";"
                   | "break" ";"
                   | "return" ";"
                   | "return" <expression> ";"

; --------------------------------------------------------------------------- ;

<export_statement> ::= "export" <identifier_list> ";"

<import_statement> ::= "import" <import_item_list> ";"
                     | "import" <import_item> "as" <identifier> ";"

<import_item_list> ::= <import_item>
                     | <import_item_list> "," <import_item>

<import_item> ::= <identifier>
                | <nested_name_specifier> <identifier>

<nested_name_specifier> ::= <identifier> "::"
                          | <nested_name_specifier> <identifier> "::"

; --------------------------------------------------------------------------- ;

<expression> ::= <conditional_expression>

<conditional_expression> ::= <logical_or_expression>
                           | <logical_or_expression> "?" <conditional_expression> ":" <conditional_expression>

<logical_or_expression> ::= <logical_and_expression>
                          | <logical_or_expression> "||" <logical_and_expression>

<logical_and_expression> ::= <inclusive_or_expression>
                           | <logical_and_expression> "&&" <inclusive_or_expression>

<inclusive_or_expression> ::= <exclusive_or_expression>
                            | <inclusive_or_expression> "|" <exclusive_or_expression>

<exclusive_or_expression> ::= <and_expression>
                            | <exclusive_or_expression> "^" <and_expression>

<and_expression> ::= <equality_expression>
                   | <and_expression> "&" <equality_expression>

<equality_expression> ::= <relational_expression>
                        | <equality_expression> <equality_operator> <relational_expression>

<equality_operator> ::= "==" | "!="

<relational_expression> ::= <shift_expression>
                          | <relational_expression> <relational_operator> <shift_expression>

<relational_operator> ::= "<" | ">" | "<=" | ">="

<shift_expression> ::= <additive_expression>
                     | <shift_expression> <shift_operator> <additive_expression>

<shift_operator> ::= "<<" | ">>"

<additive_expression> ::= <multiplicative_expression>
                        | <additive_expression> <additive_operator> <multiplicative_expression>

<additive_operator> ::= "+" | "-"

<multiplicative_expression> ::= <unary_expression>
                              | <multiplicative_expression> <multiplicative_operator> <unary_expression>

<multiplicative_operator> ::= "*" | "/" | "%"

<unary_expression> ::= <postfix_expression>
                     | <constant>
                     | <unary_operator> <unary_expression>

<unary_operator> ::= <arithmetic_unary_operator>
                   | <logical_unary_operator>

<arithmetic_unary_operator> ::= '+' | '-' | '~'

<logical_unary_operator> ::= '!'

<postfix_expression> ::= <variable>
                       | "(" <expression> ")"
                       | <lambda>
                       | <nested_name_specifier> <identifier>
                       | <postfix_expression> "(" <expression_list> ")"
                       | <postfix_expression> "(" ")"

<lambda> ::= <function_type> <compound_statement>

<function_type> ::= "func" "(" <optional_identifier_list> ")"

<expression_list> ::= <expression>
                    | <expression_list> "," <expression>

<constant> ::= <integer>
             | <floating_point>
             | <literal_string>

; --------------------------------------------------------------------------- ;

<enum_declaration> ::= "enum" "{" <optional_enum_member_list> "}"

<optional_enum_member_list> ::= ""
                              | <enum_member_list>
                              | <enum_member_list> ','

<enum_member_list> ::= <identifier>
                     | <identifier> '=' <integer>
                     | <enum_member_list> "," <identifier>
                     | <enum_member_list> "," <identifier> "=" <integer>

; --------------------------------------------------------------------------- ;

<class_definition> ::= "class" <identifier> "{" <optional_class_member_list> "}"

<optional_class_member_list> ::= ""
                               | <optional_class_member_list> <class_member>

<class_member> ::= <member_function_definition>
                 | <variable_declaration_statement>

<member_function_definition> ::= <member_function_declaration> <compound_statement>

<member_function_declaration> ::= "func" <identifier> "(" <member_function_parameter_list> ")"

<member_function_parameter_list> ::= "self"
                                   | "self" "," <identifier_list>
                                   | <optional_identifier_list>

; --------------------------------------------------------------------------- ;

<function_definition> ::= <function_declaration> <compound_statement>

<function_declaration> ::= "func" <identifier> "(" <optional_identifier_list> ")"

; --------------------------------------------------------------------------- ;

<optional_identifier_list> ::= ""
                             | <identifier_list>

<identifier_list> ::= <identifier>
                    | <identifier_list> "," <identifier>

<identifier> ::= <character_of_identifier> <character_or_digit_sequence>

<character_or_digit_sequence> ::= ""
                                | <character_or_digit_sequence> <character_of_identifier>
                                | <character_or_digit_sequence> <digit>

<character_of_identifier> ::= "_"
                            | "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m"
                            | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z"
                            | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M"
                            | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"

; --------------------------------------------------------------------------- ;

<integer> ::= <digit_sequence>
            | <digit_sequence> <exponent_specifier> <positive_plain_integer>

<positive_plain_integer> ::= <digit_sequence>
                           | "+" <digit_sequence>

<negative_plain_integer> ::= "-" <digit_sequence>

<floating_point> ::= <fractional>
                   | <fractional> <exponent_specifier> <digit_sequence>
                   | <digit_sequence> <exponent_specifier> <negative_plain_integer>

<fractional> ::= <digit_sequence> "." <digit_sequence>

<exponent_specifier> ::= "e" | "E"

<digit_sequence> ::= <digit>
                   | <digit_sequence> <digit>

<digit> ::= "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"

; --------------------------------------------------------------------------- ;

<literal_string> ::= "\"" <non-quote-characters> "\""
